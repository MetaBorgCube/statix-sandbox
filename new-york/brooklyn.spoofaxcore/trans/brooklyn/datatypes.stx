module brooklyn/datatypes

imports
  signatures/types-sig
  canarsie/types

signature
 
  constructors
    TVarCharI : int -> DataType

rules // data type

  typeOfDataType: DataType -> TYPE
  
  typeOfDataType(TVarChar()) 	= VARCHAR(80).
  typeOfDataType(TVarCharI(n))	= VARCHAR(n).
  typeOfDataType(TInt()) 		= INT().
  typeOfDataType(TDate()) 		= DATE().

rules

  equiType: TYPE * TYPE

  equiType(FK(T1, _), T2) :- equiType(T1, T2).
  equiType(T1, FK(T2, _)) :- equiType(T1, T2).

  equiType(VARCHAR(_), VARCHAR(_)).
  // Indirection to solve overlapping pattern issue
  equiType(T1, T2) :- T1 == T2
  | error $[Types [T1] and [T2] cannot be compared].

rules

  columnReferenceTypeOk: TYPE * TYPE

  columnReferenceTypeOk(FK(T, _), T).
  columnReferenceTypeOk(FK(VARCHAR(s1), _), VARCHAR(s2)) :-
    s1 #> s2.
  
  columnReferenceTypeOk(_, _) :- false.