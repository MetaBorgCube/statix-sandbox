module statics/canarsie

imports canarsie/types
imports canarsie/resolution
imports canarsie/procedures
imports statics/base
imports statics/type
imports statics/builtin
imports statics/numbers
imports statics/strings
imports statics/function
imports statics/record
imports statics/variable
imports statics/mod

rules // Performs mapping of sql table types to the types that are used in mod

  proj(TABLE(s_table), n) = strictTypeOf(s_col) :-
    resolveCol(s_table, n) == s_col.
    
  scopeOfType(TABLE(s_table)) = s_table.

  strict(VARCHAR(_)) = STRING().
  strict(DATE()) = dateType().
  
  strict(RESULT(s)) = T@REC(s_rec) :-
    new s_rec,
    declHd(s_rec, s),
    declTl(s_rec, T).
  
  // Indirection for debugging
  declHd: scope * scope
  declHd(s_rec, s_res) :-
    !var["hd", withType(TABLE(s_res))] in s_rec.
  
  declTl: scope * TYPE
  declTl(s_rec, T) :-
    !var["tl", withType(T)] in s_rec.

  strict(FK(_, T)) = strict(T).
  
  strict(PROC(P, R)) = curry(P, R).

  curry: list(TYPE) * TYPE -> TYPE
  curry([], R) = strict(R).
  curry([H|T], R) = FUN(strict(H), curry(T, R)).
  